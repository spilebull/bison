apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'deploygate'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.cotetsu.bison"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Lint Error 時でも ビルド処理を継続
    lintOptions {
        abortOnError false
    }

    // DeployGate 自動ビルド設定
    deploygate {
        // アプリ所有者 [ユーザー名] or [グループ名]
        userName = "$System.env.DEPLOYGATE_USER"
        // [ユーザー] or [グループ] の [API Key]
        token = "$System.env.DEPLOYGATE_TOKEN"

        // 追加設定（フレーバー毎）
        apks {
            // `debug` フレーバーに対応し `uploadDeployGateDebug` タスクで使用
            debug {
                // 対象のファイルを設定
                sourceFile = file("${project.rootDir}/app/build/outputs/apk/debug/app-debug.apk")
                // 便利情報: 現在のコミットの git のハッシュ を使うとトラブルシューティングを楽にできます
                def hash = 'git rev-parse --short HEAD'.execute([], project.rootDir).in.text.trim()
                // ビルドのメッセージとして設定
                message = "debug build ${hash}"
                // `Git` コミットログ取得
                def log = 'git log -n 5 --oneline | cut -c 9-'.execute().text
                // 配布ページを利用している場合、以下の設定で同時に更新可
                distributionKey = "$System.env.DEPLOYGATE_DISTRIBUTION_KEY"
                releaseNote = "${log}"
            }
        }
    }
}
// Jacoco 出力設定
jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

dependencies {
    // --------- Default Library --------------------
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$ver_kotlin"
    implementation "androidx.appcompat:appcompat:$ver_androidx"
    implementation "androidx.core:core-ktx:$ver_androidx"
    implementation "androidx.constraintlayout:constraintlayout:$ver_androidx_constraintlayout"

    // --------- Test -------------------------------
    testImplementation "junit:junit:$ver_test_junit"
    androidTestImplementation "androidx.test:runner:$ver_test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$ver_test_espresso"
}
